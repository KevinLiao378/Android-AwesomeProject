plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        consumerProguardFiles "consumer-rules.pro"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    api 'com.facebook.react:react-native:0.69.7'

    if (isEnableHermes()) {
        //noinspection GradleDynamicVersion
        api("com.facebook.react:hermes-engine:0.69.7") { // From node_modules
            exclude group:'com.facebook.fbjni'
        }
    } else {
        api 'org.webkit:android-jsc:+'
    }
    // RN调试工具（只在debug下依赖）
    def FLIPPER_VERSION= "0.125.0"
    debugImplementation("com.facebook.flipper:flipper:${FLIPPER_VERSION}") {
        exclude group:'com.facebook.fbjni'
    }
    debugImplementation("com.facebook.flipper:flipper-network-plugin:${FLIPPER_VERSION}") {
        exclude group:'com.facebook.flipper'
        exclude group:'com.squareup.okhttp3', module:'okhttp'
    }
    debugImplementation("com.facebook.flipper:flipper-fresco-plugin:${FLIPPER_VERSION}") {
        exclude group:'com.facebook.flipper'
    }
}

def isEnableHermes() {
    def enableHermes = false
    rootProject.subprojects.forEach { project ->
        if (project.plugins.hasPlugin("com.android.application")) {
            if(project.ext.react.enableHermes) {
                enableHermes = true
            }
        }
    }
    return enableHermes
}